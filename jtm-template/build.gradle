import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'eclipse'

ext {
    major = 0
    minor = 0
    patch = 0

    moduleName = project.name
    replacements = [
            moduleName: moduleName
    ]
}

version = major + "." + minor + "." + patch

repositories {
    flatDir {
        dirs 'lib'
    }
    maven {
        credentials {
            username "$caplinNexusUser"
            password "$caplinNexusSecret"
        }
        url "http://nexus.caplin.com:8081/caplin-release"
    }
}

dependencies {
    compile(group: 'com.caplin.platform.components', name: 'JTM', version: '7.0.+')
}

jar {
    version = null
}

task createKit(type: Zip, dependsOn: [jar, javadoc]) {
    archiveName = moduleName + "-" + version + ".zip"
    def topDirName = archiveName.substring(0, archiveName.lastIndexOf("."))

    into("${topDirName}") {
        from "${project.projectDir}/blade"
        rename 'module.conf', "${moduleName}.conf"
        filter(ReplaceTokens, tokens: replacements)
    }

    into("${topDirName}/Transformer/lib/java") {
        from([project.jar.outputs, configurations.compile])
    }
}

assemble.dependsOn(createKit)
