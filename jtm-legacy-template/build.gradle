import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'eclipse'

ext {
    major = 0
    minor = 0
    patch = 0

    moduleName = rootProject.name
    replacements = [
            moduleName: moduleName
    ]
}

version = major + "." + minor + "." + patch

repositories {
    maven {
        credentials {
            username "$caplinNexusUser"
            password "$caplinNexusSecret"
        }
        url "https://repository.caplin.com"
    }
}

dependencies {
    //implementation fileTree(dir: 'lib', include: '*.jar')

    implementation(group: 'com.caplin.platform.components', name: 'TransformerModule', version: '7.0.+')
}

jar {
    version = null
}

task createKit(type: Zip, dependsOn: [jar, javadoc]) {
    archiveName = moduleName + "-" + version + ".zip"
    def topDirName = archiveName.substring(0, archiveName.lastIndexOf("."))

    into("${topDirName}") {
        from "${project.projectDir}/blade"
        rename 'module.conf', "${moduleName}.conf"
        filter(ReplaceTokens, tokens: replacements)
    }

    into("${topDirName}/Transformer/lib/java") {
        from([project.jar.outputs, configurations.compileClasspath])
    }
}

assemble.dependsOn(createKit)
